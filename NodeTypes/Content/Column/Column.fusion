prototype(KaufmannDigital.EmailEditing:Content.Column) < prototype(KaufmannDigital.EmailEditing:ContentComponent.DualRendering) {

  componentProps = Neos.Fusion:DataStructure {
    templateNode = KaufmannDigital.EmailEditing:TemplateNodeSelector

    backgroundColor = ${q(node).property('backgroundColor') || q(this.templateNode).property('columnBackgroundColor') || null}
    padding = ${q(node).property('padding') || q(this.templateNode).property('columnPadding') || null}
    width = ${q(node).property('width') || '100%'}
  }

  @context.componentProps = ${this.componentProps}

  mjmlRendering {
    renderer = KaufmannDigital.EmailEditing:Component.Column {
      @apply.componentProps = ${componentProps}

      content = Neos.Neos:ContentCollection
    }
  }

  htmlRendering {

    inlineStyles = Neos.Fusion:DataStructure {
      width = ${componentProps.width || (100 / q(q(node).parents().get(0)).children('[instanceof KaufmannDigital.EmailEditing:Content.Column]').count() + '%')}

      background-color = ${componentProps.backgroundColor}
      background-color.@if.isSet = ${componentProps.backgroundColor}

      padding = ${componentProps.padding}
      padding.@if.isSet = ${componentProps.padding}
    }

    content = Neos.Neos:ContentCollection
    renderer = afx`
      <div class="email-column">
        {props.content}
      </div>
    `
  }

@process.contentElementWrapping >
}
