prototype(KaufmannDigital.EmailEditing:Content.Button) < prototype(KaufmannDigital.EmailEditing:ContentComponent.DualRendering) {

  @context {
    componentProps = Neos.Fusion:DataStructure {
      padding = ${q(node).property('padding')}
      padding.@if.notEmpty = ${q(node).property('padding')}
      backgroundColor = ${q(node).property('backgroundColor') || '#414141'}
      color = ${q(node).property('fontColor') || '#FFFFFF'}
      align = ${q(node).property('align') || 'left'}
      href = ${q(node).property('target') || ''}
      href.@process.convertUri = Neos.Neos:ConvertUris
    }
  }

  mjmlRendering {
    renderer = KaufmannDigital.EmailEditing:Component.Button {
      @apply.componentProps = ${componentProps}

      label = ${label}
    }
  }

  htmlRendering {

    label = Neos.Neos:Editable {
      property = 'buttonLabel'
    }

    renderer = KaufmannDigital.EmailEditing:MjmlElementRenderer {

      backgroundColor = ${q(q(node).parents('[backgroundColor]').get(0)).property('backgroundColor')}
      mjmlSource = KaufmannDigital.EmailEditing:Component.Button {
        @apply.componentProps = ${componentProps}
        label = ${props.label}
      }
    }
  }
}
