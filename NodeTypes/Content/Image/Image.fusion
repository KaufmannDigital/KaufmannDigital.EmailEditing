prototype(KaufmannDigital.EmailEditing:Content.Image) < prototype(KaufmannDigital.EmailEditing:ContentComponent.DualRendering) {

  @context {
    componentProps = Neos.Fusion:DataStructure {
      imageUrl = Neos.Neos:ImageUri {
        asset = ${q(node).property('image')}
      }

      width = ${q(node).property('imageWidth')}
      padding = ${q(node).property('imagePadding')}
      align = ${q(node).property('align')}
      href = ${node.context.inBackend ? null : q(node).property('href')}
    }
  }

  mjmlRendering {
    renderer = KaufmannDigital.EmailEditing:Component.Image {
      @apply.componentProps = ${componentProps}
    }
  }

  htmlRendering {
    renderer = KaufmannDigital.EmailEditing:MjmlElementRenderer {
      backgroundColor = ${FallbackValue.contentBackgroundColor(node)}
      mjmlSource = KaufmannDigital.EmailEditing:Component.Image {
        @apply.componentProps = ${componentProps}
      }
    }
  }

}
