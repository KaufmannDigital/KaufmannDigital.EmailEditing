prototype(KaufmannDigital.EmailEditing:Document.EmailLayout) < prototype(Neos.Fusion:Component) {

  content = ''

  logoUrl = Neos.Neos:ImageUri {
    asset = ${q(node).property('logo')}
  }

  renderer = Neos.Neos:Page {
    head {
      stylesheets = afx`
        <link rel="stylesheet" type="text/css" href={StaticResource.uri('KaufmannDigital.EmailEditing', 'Public/Styles/EmailEditing.css')}/>
      `
    }

    body = Neos.Fusion:Tag {
      attributes.class = 'email-editing-page'

      content = Neos.Fusion:Join {
        header = KaufmannDigital.EmailEditing:MjmlElementRenderer {
          mjmlSource = afx`
            <mj-section full-width="full-width">
              <mj-column width="66.66666666666666%" vertical-align="middle">
                <mj-text align="left" font-size="11px" padding-bottom="0px" padding-top="0"><span style="font-size: 11px">[[HEADLINE]]</span></mj-text>
              </mj-column>
              <mj-column width="33.33333333333333%" vertical-align="middle">
                <mj-text align="right" font-size="11px" padding-bottom="0px" padding-top="0"><span style="font-size: 11px"><a href="https://mjml.io" style="text-decoration: none; color: inherit;">[[PERMALINK_LABEL]]</a></span></mj-text>
              </mj-column>
            </mj-section>
            <mj-section background-color="#ffffff" full-width="full-width">
              <mj-column width="33.33333333333333%" vertical-align="middle">
                <mj-image src={props.logoUrl} alt="OnePage" padding-bottom="0px" padding-top="10px"></mj-image>
              </mj-column>
              <mj-column width="66.66666666666666%"></mj-column>
            </mj-section>
          `
        }

        content = ${props.content}
        contentPlaceholder = '<div style="padding: 100px 0; text-align: center;">Content goes here</div>'
        contentPlaceholder.@if.emptyContent = ${props.content == ''}
      }
    }
  }
}
